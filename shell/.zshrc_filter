#!/bin/zsh
alias filter="${FILTER_COMMAND}"

if [ -z "$FILTER_COMMAND" ]; then
  echo "FILTER_COMMAND variable is empty;"
  return;
fi

alias -g F='| filter'

# http://stillpedant.hatenablog.com/entry/percol-cd-history
typeset -U chpwd_functions
export CD_HISTORY_FILE=${HOME}/.cd_history_file # cd 履歴の記録先ファイル
function chpwd_record_history() {
  echo $PWD >> ${CD_HISTORY_FILE}
}
chpwd_functions=($chpwd_functions chpwd_record_history)

  # filter toolを使って cd 履歴の中からディレクトリを選択
  # 過去の訪問回数が多いほど選択候補の上に来る
function filter_get_destination_from_history() {
  sort ${CD_HISTORY_FILE} | uniq -c | sort -r | \
  sed -e 's/^[ ]*[0-9]*[ ]*//' | \
  sed -e s"/^${HOME//\//\\/}/~/" | \
  filter | xargs echo
}

# filter tool を使って cd 履歴の中からディレクトリを選択し cd するウィジェット
function filter_cd_history() {
  local destination=$(filter_get_destination_from_history)
  if [ "${destination}" != "" ]; then
    save_history cd ${destination/#\~/${HOME}}
    return 0;
  fi
  return -1;
}
zle -N filter_cd_history
alias fcd='filter_cd_history'

# filter tool を使って cd 履歴の中からディレクトリを選択し，現在のカーソル位置に挿入するウィジェット
# function filter_insert_history() {
# local destination=$(filter_get_destination_from_history)
# if [ $? -eq 0 ]; then
#   local new_left="${LBUFFER} ${destination} "
#   BUFFER=${new_left}${RBUFFER}
#   CURSOR=${#new_left}
# fi
# zle -N reset-prompt
# }
# zle -N filter_insert_history
# alias pins='filter_insert_history'
# }}}

# https://mogulla3.tech/articles/2021-09-06-search-command-history-with-incremental-search/
function select-history() {
  BUFFER=$(history -n -r 1 | fzf --exact --reverse --no-sort --query="$LBUFFER" --cycle --prompt="History > ")
  CURSOR=${#BUFFER}
  # zle accept-line # 選択した履歴を即座に実行
}
zle -N select-history


alias fgsw='filter_git_command_fmt git switch'
alias fgmg='filter_git_command git merge'
alias fgmgs='fgmg --squash'
alias fgpl='filter_git_command_fmt git pull origin'
alias fgps='filter_git_command_fmt git push origin'
alias fgbd='filter_git_command_fmt git branch -d'
alias fgbD='filter_git_command_fmt git branch -D'
alias fgb='br_org'
alias fgbd_remote='filter_git_command_fmt git push --delete origin'
alias fgrb='filter_git_command_fmt git pull --rebase origin'
alias fgl='filter_git_command git log'
alias fglp='fgl -p'
alias fgd='filter_git_command git diff'
alias fgdn='fgd --name-status'
alias fglst='fgl --stat'
# alias fgsw-pr='gh pr list | awk \\'{print $(NF-1)}\\' | filter | xargs git switch'

alias fls='ls -aR | filter --preview "bat --color=always --style=header,grid {}"'
alias ffind='find -L . -name "*" | filter --preview "bat --color=always --style=header,grid {}"'
alias fps='ps aux | filter'
alias fkill='ps ax | filter | awk "{ print $1 }" | xargs kill'
alias fopen='filter_find_command open'
# alias fless='ffind | xargs less'
alias fless='filter_find_command less'
alias fnvim='filter_find_command nvim'
# http://qiita.com/itkrt2y/items/0671d1f48e66f21241e2
alias fghq='save_history cd $(ghq root)/$(ghq list | filter)'

function fcat(){
  cat -n $@ | filter
}

# git switch -c by branch filter
function fgswc(){
  local temp=`br_org | xargs echo`
  if [[ -n $temp ]]
  then
    save_history gswc "$@" "$temp"
  fi
}

# Gitのブランチをfilter toolで扱えるように整形(remote文字列を削る)
function br_fmt(){
  br_org | sed -e 's/remotes\/origin\///'
}

# Gitのブランチをfilter toolで扱えるように整形
function br_org(){
  # 取り込まれたコミットのタイムスタンプを降順（-）でソート
  # ログをいい感じに表示 https://zenn.dev/yamo/articles/5c90852c9c64ab
  git branch -a --sort=-committerdate --color \
    | filter --preview "echo {} | sed -e 's/\*//' | awk '{print \$1}' | xargs git log --color --graph --decorate --abbrev-commit --format=format:'%C(blue)%h%C(reset) - %C(green)(%ar)%C(reset)%C(yellow)%d%C(reset)\n  %C(white)%s%C(reset) %C(dim white)- %an%C(reset)'" \
    | xargs echo | sed -e 's/\*//' | awk '{print $1}'
}

# br_fmtのリモート対象版
function br_fmt_remote(){
  br_fmt | xargs echo | awk '{printf("%s%s\n", "remotes/origin/", $0)}'
}

# コマンドを履歴に残す
function save_history(){
  local cmd="$@"
  eval $cmd
	if test $? -ne 0
	then
		return 1
	fi
  print -s $cmd
}

# findの結果をfilter toolで絞ってコマンドを実行する
function filter_find_command(){
  local cmd="$(ffind)"
  save_history $@ $cmd
}

# ブランチをfilter toolで絞ってgitコマンドを実行する
function filter_git_command(){
  local cmd="$@ $(br_org)"
  save_history $cmd
}
function filter_git_command_fmt(){
  local cmd="$@ $(br_fmt)"
  save_history $cmd
}

# filter tool git show file
function fgsf(){
  local temp=`br_fmt | xargs echo`
  save_history git show "${temp}":"$@"
}
# filter tool git show file
function fgsf-remote(){
  local temp=`br_fmt | xargs echo`
  save_history git show "remotes/origin/${temp}":"$@"
}

function fgsw-pr(){
  gh pr list | awk '{print $3}' | filter | xargs git switch

}

# fcd-down - cd to selected directory
# https://qiita.com/kamykn/items/aa9920f07487559c0c7e
function fcd-down() {
  local dir
  dir=$(find ${1:-.} -path '*/\.*' -prune \
                  -o -type d -print 2> /dev/null | filter +m) &&
  cd "$dir"
}

# fzfでdockerコンテナに入る
# https://yiskw713.hatenablog.com/entry/2022/01/12/200000#fgc-%E3%82%A4%E3%83%B3%E3%82%BF%E3%83%A9%E3%82%AF%E3%83%86%E3%82%A3%E3%83%96%E3%81%ABgit-checkout%E3%81%99%E3%82%8B
function fdocker-container() {
  local cid
  cid=$(docker ps | sed 1d | fzf -q "$1" | awk '{print $1}')
  [ -n "$cid" ] && docker exec -it "$cid" sh
}

# fzfでdockerのログを取得
# https://yiskw713.hatenablog.com/entry/2022/01/12/200000#fgc-%E3%82%A4%E3%83%B3%E3%82%BF%E3%83%A9%E3%82%AF%E3%83%86%E3%82%A3%E3%83%96%E3%81%ABgit-checkout%E3%81%99%E3%82%8B
function fdocker-log() {
  local cid
  cid=$(docker ps -a | sed 1d | fzf -q "$1" | awk '{print $1}')
  [ -n "$cid" ] && docker logs -f --tail=200 "$cid"
}
